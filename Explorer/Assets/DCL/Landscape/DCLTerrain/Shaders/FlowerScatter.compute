#pragma enable_d3d11_debug_symbols

#include "ScatterFunctions.cginc"

#pragma kernel FlowerScatter

struct PerInst
{
    float4 position;
    float4 quatRotation;
    float4 colour;
};

cbuffer ConstBuffer : register(b0)
{
    float4 TerrainBounds;
    int2 HeightTextureSize;
    int2 OccupancyTextureSize;
    int2 TerrainBlendTextureSize;
    int2 GroundDetailTextureSize;
    int2 SandDetailTextureSize;
    float TerrainHeight;
    int parcelSize;
}

Texture2D<float4> HeightMapTexture : register(t0);
Texture2D<float4> OccupancyTexture : register(t1);
Texture2D<float4> TerrainBlendTexture : register(t2);
Texture2D<float4> GroundDetailTexture : register(t3);
Texture2D<float4> SandDetailTexture : register(t4);
StructuredBuffer<int2> visibleParcels : register(t5);
StructuredBuffer<uint> visibleParcelCount : register(t6);

SamplerState samplerHeightMapTexture : register(s0);
SamplerState samplerOccupancyTexture : register(s1);
SamplerState HeightMapTexturePointClampSampler : register(s2);
SamplerState samplerTerrainBlendTexture : register(s3);
SamplerState TerrainBlendPointRepeatSampler : register(s4);
SamplerState samplerGroundDetailTexture : register(s5);
SamplerState samplerSandDetailTexture : register(s6);

RWStructuredBuffer<PerInst> flower0Instances : register(u0);
RWStructuredBuffer<PerInst> flower1Instances : register(u1);
shared RWStructuredBuffer<uint> drawArgs0 : register(u2);
shared RWStructuredBuffer<uint> drawArgs1 : register(u3);

[numthreads(64, 1, 1)]
void FlowerScatter(uint3 id : SV_DispatchThreadID, uint3 groupId : SV_GroupID, uint3 localId : SV_GroupThreadID)
{
    if (groupId.x >= visibleParcelCount[0])
        return;

    int2 corner0 = (int2)(visibleParcels[groupId.x] * 16.0f);
    half2 parcelGroupingArea = half2(hash_int_to_float(corner0), hash_int_to_float(-corner0));
    int2 gridPosition = int2((int)(localId.x % 8), (int)(localId.x / 8) * 2.0f);

    float2 worldSpaceCoord_ParcelBlock = corner0 + gridPosition;
    float2 UV_coords_ParcelBlock = (worldSpaceCoord_ParcelBlock + 4096.0f) / 8192.0f;

    // Check the texture
    half4 terrainColour = SplatmapMix(UV_coords_ParcelBlock * 8.0f,
        TerrainBlendTexture, samplerTerrainBlendTexture,
        GroundDetailTexture, samplerGroundDetailTexture,
        SandDetailTexture, samplerSandDetailTexture, 1);

    // Check occupancy
    float fDistanceFieldScale = 16;
    
    PerInst perInst;
    
    if (terrainColour.g < 0.1f)
    {
        half random_X_flower0 = (hash_int_to_float(corner0 + gridPosition) * 2.0f);
        half random_Z_flower0 = (hash_int_to_float(-corner0 + gridPosition) * 2.0f);

        half random_X_flower1 = random_Z_flower0;
        half random_Z_flower1 = random_X_flower0;

        random_X_flower0 += gridPosition.x;
        random_Z_flower0 += gridPosition.y;
        random_X_flower1 += gridPosition.x; 
        random_Z_flower1 += gridPosition.y; 
        
        // Get Height
        float2 UV_coords_0 = (corner0 + float2(random_X_flower0, random_Z_flower0) + 4096.0f) / 8192.0f;
        float height_flower0 = CalculateHeightFromHeightmap(UV_coords_0, fDistanceFieldScale,
            HeightMapTexture, HeightMapTexturePointClampSampler,
            OccupancyTexture, samplerOccupancyTexture);
        float2 UV_coords_1 = (corner0 + float2(random_X_flower1, random_Z_flower1) + 4096.0f) / 8192.0f;
        float height_flower1 = CalculateHeightFromHeightmap(UV_coords_1, fDistanceFieldScale,
            HeightMapTexture, HeightMapTexturePointClampSampler,
            OccupancyTexture, samplerOccupancyTexture);
    
        float fScale0 = 1.0f + hash_int_to_float(UV_coords_0);
        float fScale1 = 1.0f + hash_int_to_float(UV_coords_1);
        
        float3 normalWS_0 = CalculateNormalFromHeightmap(UV_coords_0, fDistanceFieldScale,
            HeightMapTexture, samplerHeightMapTexture,
            OccupancyTexture, samplerOccupancyTexture);
        float3 normalWS_1 = CalculateNormalFromHeightmap(UV_coords_1, fDistanceFieldScale,
            HeightMapTexture, samplerHeightMapTexture,
            OccupancyTexture, samplerOccupancyTexture);
        
        InterlockedAdd(drawArgs0[1], 1);
        perInst.position = float4(random_X_flower0, height_flower0, random_Z_flower0, fScale0);
        perInst.quatRotation = CreateGrassRotationQuaternion(normalWS_0, hash_int_to_float(corner0 + gridPosition) * 2.0f);
        perInst.colour = float4(1.0, 1.0, 1.0, 1.0);
        flower0Instances[id.x] = perInst;
        
        InterlockedAdd(drawArgs1[1], 1);
        perInst.position = float4(random_X_flower1, height_flower1, random_Z_flower1, fScale1);
        perInst.quatRotation = CreateGrassRotationQuaternion(normalWS_1, hash_int_to_float(corner0 + gridPosition) * 2.0f);
        perInst.colour = float4(1.0, 1.0, 1.0, 1.0);
        flower1Instances[id.x] = perInst;
    }
    else
    {
        perInst.position = float4(0.0f, -5000.0f, 0.0f, 0.0f);
        perInst.quatRotation = float4(0.0f, 0.0f, 0.0f, 0.0f);
        perInst.colour = float4(0.0f, 0.0f, 0.0f, 0.0f);
        InterlockedAdd(drawArgs0[1], 1);
        InterlockedAdd(drawArgs1[1], 1);
        flower0Instances[id.x] = perInst;
        flower1Instances[id.x] = perInst;
    }
}
