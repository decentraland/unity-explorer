#pragma enable_d3d11_debug_symbols

#include "ScatterFunctions.cginc"

#pragma kernel FlowerScatter

struct PerInst
{
    float4 position;
    float4 quatRotation;
    float4 colour;
};

cbuffer ConstBuffer : register(b0)
{
    float4 TerrainBounds;
    int2 HeightTextureSize;
    int2 OccupancyTextureSize;
    int2 TerrainBlendTextureSize;
    int2 GroundDetailTextureSize;
    int2 SandDetailTextureSize;
    float TerrainHeight;
    int parcelSize;
}

Texture2D<float4> HeightMapTexture : register(t0);
Texture2D<float4> OccupancyTexture : register(t1);
Texture2D<float4> TerrainBlendTexture : register(t2);
Texture2D<float4> GroundDetailTexture : register(t3);
Texture2D<float4> SandDetailTexture : register(t4);
StructuredBuffer<int2> visibleParcels : register(t5);
StructuredBuffer<uint> visibleParcelCount : register(t6);

SamplerState samplerHeightMapTexture : register(s0);
SamplerState samplerOccupancyTexture : register(s1);
SamplerState HeightMapTexturePointClampSampler : register(s2);
SamplerState samplerTerrainBlendTexture : register(s3);
SamplerState TerrainBlendPointRepeatSampler : register(s4);
SamplerState samplerGroundDetailTexture : register(s5);
SamplerState samplerSandDetailTexture : register(s6);

RWStructuredBuffer<PerInst> flower0Instances : register(u0);
RWStructuredBuffer<PerInst> flower1Instances : register(u1);
shared RWStructuredBuffer<uint> drawArgs0 : register(u2);
shared RWStructuredBuffer<uint> drawArgs1 : register(u3);

[numthreads(64, 1, 1)]
void FlowerScatter(uint3 id : SV_DispatchThreadID, uint3 groupId : SV_GroupID, uint3 localId : SV_GroupThreadID)
{
    if (groupId.x >= visibleParcelCount[0])
        return;

    int2 corner0 = (int2)(visibleParcels[groupId.x] * 16.0f);
    half2 parcelGroupingArea = half2(hash_int_to_float(corner0), hash_int_to_float(-corner0));
    int2 gridPosition = int2((int)((localId.x * 4) % 16), (int)(localId.x / 16) * 4);
    half2 fGridPosition = gridPosition * 0.25f;

    half random_X = (hash_int_to_float(corner0 + gridPosition) * 2.0f) + fGridPosition.x;
    half random_Z = (hash_int_to_float(-corner0 + gridPosition) * 2.0f) + fGridPosition.y;

    float2 worldSpaceCoord = corner0 + float2(random_X, random_Z);
    float2 UV_coords = (worldSpaceCoord + 4096.0f) / 8192.0f;

    // Check occupancy
    float fDistanceFieldScale = 16;

    // Get Height
    float height = CalculateHeightFromHeightmap(UV_coords, fDistanceFieldScale, HeightMapTexture, HeightMapTexturePointClampSampler, OccupancyTexture, samplerOccupancyTexture);

    // Check the texture
    half4 terrainColour = SplatmapMix(UV_coords * 8.0f, TerrainBlendTexture, samplerTerrainBlendTexture,
        GroundDetailTexture, samplerGroundDetailTexture,
        SandDetailTexture, samplerSandDetailTexture);

    PerInst perInst;
    float fScale = 1.0f + hash_int_to_float(corner0 + gridPosition);
    perInst.position = float4(random_X, height, random_Z, fScale);

    float3 normalWS = CalculateNormalFromHeightmap(UV_coords, fDistanceFieldScale,  HeightMapTexture, samplerHeightMapTexture, OccupancyTexture, samplerOccupancyTexture);
    perInst.quatRotation = CreateGrassRotationQuaternion(normalWS, hash_int_to_float(corner0 + gridPosition) * 2.0f);
    perInst.colour = float4(1.0, 1.0, 1.0, 1.0);

    InterlockedAdd(drawArgs0[1], 1);
    InterlockedAdd(drawArgs1[1], 1);
    flower0Instances[id.x] = perInst;
    perInst.position.xyz += float3(parcelGroupingArea.x, 0.0f,parcelGroupingArea.y);
    flower1Instances[id.x] = perInst;
}
